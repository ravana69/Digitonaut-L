<!DOCTYPE html>
<html lang="en" >

<!-- Mirrored from digitonaut.com/art/lines_and_circles/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 12:46:00 GMT -->
<!-- Added by HTTrack --><meta http-equiv="content-type" content="text/html;charset=UTF-8" /><!-- /Added by HTTrack -->
<head>
<meta charset='utf-8'\>
<meta name='apple-mobile-web-app-capable' content='yes'><meta name='apple-mobile-web-app-status-bar-style' content='black-translucent'><title>Lines And Circles</title>

<style>
canvas {
	position:fixed;
	left:50%; top:50%;
	transform:translate(-50%,-50%);
	border:0px; outline:0px;
}
</style>
</head>
<body style='margin:0px;overflow:hidden;background:#000;'>

<script type="application/processing">
int nbCircles = 6;
Circle[] circles;
MyColor myColor;
float rMax, dMin;

void setup()
{
  //size(640, 480, P2D);
size(screenWidth,screenHeight);
  frameRate(50);
  fill(0, 60);
  rMax = min(width, height)/2;
  dMin = max(width, height)/3.6;
  circles = new Circle[nbCircles];
  initialize(false);
}

void initialize(Boolean p_random)
{ 
  for (int i = 0; i < nbCircles; i++)
  {
    circles[i] = new Circle(random(rMax), 
    p_random ? random(-width/3, width/3) : 0, 
    p_random ? random(-height/3, height/3) : 0);
  }
  myColor = new MyColor();
}

void draw()
{
  noStroke();
  rect(0, 0, width, height);
  translate(width/2, height/2);
  myColor.update();
  for (int j = 0; j < nbCircles; j++)
  {
    circles[j].update();
    for (int i = j+1; i < nbCircles; i++)
    {
      connect(circles[j], circles[i]);
    }
  }
}

void connect(Circle c1, Circle c2)
{
  float d, x1, y1, x2, y2, r1 = c1.radius, r2 = c2.radius;
  float rCoeff = map(min(abs(r1), abs(r2)), 0, rMax, .08, 1);
  int n1 = c1.nbLines, n2 = c2.nbLines;
  for (int i = 0; i < n1; i++)
  {
    x1 = c1.x + r1 * cos(i * TWO_PI / n1 + c1.theta);
    y1 = c1.y + r1 * sin(i * TWO_PI / n1 + c1.theta);
    for (int j = 0; j < n2; j++)
    {
      x2 = c2.x + r2 * cos(j * TWO_PI / n2 + c2.theta);
      y2 = c2.y + r2 * sin(j * TWO_PI / n2 + c2.theta);

      d = dist(x1, y1, x2, y2);
      if (d < dMin)
      {
        stroke(myColor.R + r2/1.5, myColor.G + r2/2.2, myColor.B + r2/1.5, map(d, 0, dMin, 140, 0) * rCoeff);
        line(x1, y1, x2, y2);
      }
    }
  }
}

void mousePressed()
{
  initialize(mouseButton == RIGHT);
}

class Circle
{
  float x, y, radius, theta = 0;
  int nbLines = (int)random(3, 25);
  float rotSpeed = (random(1) < .5 ? 1 : -1) * random(.005, .034);
  float radSpeed = (random(1) < .5 ? 1 : -1) * random(.3, 1.4);
  

  Circle(float p_radius, float p_x, float p_y)
  {
    radius = p_radius;
    x = p_x;
    y = p_y;
  }

  void update()
  {
    theta += rotSpeed;
    radSpeed *= abs(radius += radSpeed) > rMax ? -1 : 1;
  }
}

class MyColor
{
  float R, G, B, Rspeed, Gspeed, Bspeed;
  final static float minSpeed = .2;
  final static float maxSpeed = .8;
  MyColor()
  {
    R = random(2, 255);
    G = random(2, 255);
    B = random(2, 255);
    Rspeed = (random(1) > .5 ? 1 : -1) * random(minSpeed, maxSpeed);
    Gspeed = (random(1) > .5 ? 1 : -1) * random(minSpeed, maxSpeed);
    Bspeed = (random(1) > .5 ? 1 : -1) * random(minSpeed, maxSpeed);
  }

  public void update()
  {
    Rspeed = ((R += Rspeed) > 255 || (R < 20)) ? -Rspeed : Rspeed;
    Gspeed = ((G += Gspeed) > 255 || (G < 20)) ? -Gspeed : Gspeed;
    Bspeed = ((B += Bspeed) > 255 || (B < 20)) ? -Bspeed : Bspeed;
  }
}
</script>

<canvas> </canvas>
<script src='processing.min.js'></script>
</body>

<!-- Mirrored from digitonaut.com/art/lines_and_circles/ by HTTrack Website Copier/3.x [XR&CO'2014], Thu, 16 Feb 2023 12:46:03 GMT -->
</html>
